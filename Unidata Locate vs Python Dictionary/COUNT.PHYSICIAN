*Program: COUNT.PHYSICIAN
*Purpose: Compare UDT Locate vs Python Dictionary

TIME.0 = SYSTEM(12); * capture start time
*
KEYS.LIST = ""
COUNT.LIST = ""
KEYS.COUNT = ""
TOTAL.COUNT = 0
*
OPEN "PHYSICIAN" TO F.PHYS ELSE STOP "FILE: PHYSICIAN"
*
SELECT F.PHYS
LOOP
  READNEXT ID ELSE EXIT
  READ PHYS.REC FROM F.PHYS,ID ELSE
    CRT "ERROR: Unable to read PHYSICIAN ":ID
    CONTINUE
  END
*
  GENDER = PHYS.REC<8>
  SCHOOL = PHYS.REC<10>
  YEAR = PHYS.REC<11>
  SPECIALTY = PHYS.REC<12>
  CITY = PHYS.REC<25>
  STATE = PHYS.REC<26>
*
*  USE.KEY = SPECIALTY:STATE
  USE.KEY = STATE
  TOTAL.COUNT += 1
*
  POS = 0
  LOCATE(USE.KEY,KEYS.LIST,1;POS;"AL") ELSE
    KEYS.COUNT += 1
    KEYS.LIST = INSERT(KEYS.LIST,1,POS,0,USE.KEY)
    KEYS.LIST = INSERT(KEYS.LIST,2,POS,0,0)
  END
  KEYS.LIST<2,POS> += 1
REPEAT
*
REC.COUNT = DCOUNT(KEYS.LIST<1>,@VM)
FOR KK = 1 TO REC.COUNT
  POS = 0
  LOCATE(KEYS.LIST<2,KK>,COUNT.LIST,1;POS;"DR") ELSE
    COUNT.LIST = INSERT(COUNT.LIST,1,POS,0,KEYS.LIST<2,KK>)
    COUNT.LIST = INSERT(COUNT.LIST,2,POS,0,KEYS.LIST<1,KK>)
  END
NEXT KK
*
MAX.DISPLAY = 10
FOR CC = 1 TO MAX.DISPLAY
  CRT COUNT.LIST<2,CC>:" => ":COUNT.LIST<1,CC>
NEXT CC
*
CRT ""
CRT "Record count: ":TOTAL.COUNT
CRT ""
CRT "Keys count: ":KEYS.COUNT
*
TIME.1 = SYSTEM(12); * capture end time
TIME.DIFF = TIME.1 - TIME.0
*
CRT ""
CRT "Time: ":TIME.DIFF:"ms":"  or  ":(TIME.DIFF / 1000):"s"
CRT ""
CRT ""
